// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.3.72"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.0.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

ext.versions = [
        "dagger"          : "2.26",
        "truth"           : "1.0.1",
        "recyclerview"    : "1.1.0",
        "coil"            : "0.11.0",
        "constraintlayout": "1.1.3",
        "lifecycle"       : "2.2.0",
        "archtesting"     : "2.1.0",
        "fragment"        : "1.2.5",
        "coroutines"      : "1.3.9",
        "retrofit"        : "2.8.1",
        "moshi"           : "1.9.2",
]

ext.deps = [
        "androidx"  : [
                "recyclerview"    : "androidx.recyclerview:recyclerview:${versions.recyclerview}",
                "constraintlayout": "androidx.constraintlayout:constraintlayout:${versions.constraintlayout}",
                "viewmodel"       : "androidx.lifecycle:lifecycle-viewmodel-ktx:${versions.lifecycle}",
                "livedata"        : "androidx.lifecycle:lifecycle-livedata-ktx:${versions.lifecycle}",
                "archtesting"     : "androidx.arch.core:core-testing:${versions.archtesting}",
                "fragment"        : "androidx.fragment:fragment-ktx:${versions.fragment}",
                "fragmenttesting" : "androidx.fragment:fragment-testing:${versions.fragment}"
        ],
        "coil"      : "io.coil-kt:coil:${versions.coil}",
        "coroutines": [
                "android": "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.coroutines}",
                "testing": "org.jetbrains.kotlinx:kotlinx-coroutines-test:${versions.coroutines}",
        ],
        "dagger"    : [
                "core"    : "com.google.dagger:dagger:${versions.dagger}",
                "compiler": "com.google.dagger:dagger-compiler:${versions.dagger}"
        ],
        "truth"     : "com.google.truth:truth:${versions.truth}",
        "moshi"     : [
                "core"  : "com.squareup.moshi:moshi:${versions.moshi}",
                "kotlin": "com.squareup.moshi:moshi-kotlin-codegen:${versions.moshi}"
        ],
        "retrofit"  : [
                "core" : "com.squareup.retrofit2:retrofit:${versions.retrofit}",
                "moshi": "com.squareup.retrofit2:converter-moshi:${versions.retrofit}"
        ],
]

subprojects {
    plugins.withType(com.android.build.gradle.BasePlugin).whenPluginAdded {
        android {
            compileOptions {
                sourceCompatibility = JavaVersion.VERSION_1_8
                targetCompatibility = JavaVersion.VERSION_1_8
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}